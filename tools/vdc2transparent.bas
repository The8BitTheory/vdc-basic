#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC,uppercase,10,10
# TRANSFORMS BITMAP INTO VDC-FILE WITH TRANSPARENCY
# THE TRANSPARENCY IS JUST A RESULT OF COMPLEX METADATA THAT
#  TELLS VDC-BASIC WHICH BYTES TO LEAVE OUT

# 4.64 MINUTES WRITING TO VRAM
# 4.16 MINUTES POKING AND THEN WRITING TO VRAM

# LOAD VDC-FILE
# - COPY IMAGE-DATA TO VRAM --> START AND END (OR SIZE) OF IMAGE DATA NEEDED
# - COPY META-DATA TO SOMEWHERE ELSE IN RAM --> SIZE NEEDED

# VDC FILE FORMAT (FOR FILES WITH TRANSPARENCY)
# - METADATA-LENGTH: 2 BYTES
# - IMAGEDATA-LENGTH: 2 BYTES (IMAGEDATA START IS AFTER METADATA-SIZE)
# - METADATA
# - IMAGEDATA



10 GRAPHIC 0
# DEFINE FUNCTIONS
20 GOSUB 600
30 BS=FN W(45)
40 BE=FN W(4624)
# DB=DATA BEGIN
# DE=DATA END
50 DB=BE:DE=DB+8000

60 PRINT "BASIC FROM "BS" TO "BE"."
70 PRINT "LEAVES "FRE(0)" BYTES FREE."
80 DD=PEEK(186)

90 PRINT "INSERT DATADISK":GETKEY I$

100 REM BLOAD "VDCBASIC2F.1300",B0,U(DD)
110 SYS DEC("1300")

120 AP=8000:DISP 16000

# 2 SCANLINES PER CHAR, 160 SCANLINES (EACH VAL +1)
130 RGW 0,127:RGW 4,155:RGW 6,100:RGW 7,140:RGW 9,1
140 RGO 25,128:REM BITMAP MODE ON
150 RGO 28,2î¸ž4:REM 64K VRAM
160 RGW 36,0
170 RGW DEC("1A"),DEC("FF")

180 VMF 16000,15,16000:REM SETUP PIXELS
190 VMF 0,0,16000: REM CLEAR ATTRIBUTE RAM
200 ATTR 8000:BANK 0

210 BLOAD"LANDSCAPE.VDC",B0,P(BE),U(DD)
220 RTV BE,0,8000

#230 FOR FI=0TO3
#240  PF$="RED0"+MID$(STR$(FI),2)+"A.VDC"
#250  GOSUB 290
#260  IF FI<3 THEN PRINT "PRESS KEY FOR NEXT IMAGE":GETKEY I$
#270 NEXT
230 PF$="3A.VDC":GOSUB290
280 BANK15:END


290 PRINT "LOADING "PF$" ... ";

300 BLOAD (PF$),B0,P(DB),U(DD)
310 FL=PEEK(174)+PEEK(175)*256-BE:?"DONE. LENGTH:"FL
320 PO=BE+FL:SP=PO
330 PRINT "START PO="PO", $"HEX$(PO)

#   LEAVE FIRST SIX POKE ADDRESSES FREE
#    0-1: METADATA-LENGTH
#    2-3: IMAGEDATA-LENGTH
#    4-5: ALPHA COLOR AND THE VALUE OF CC AT THE END (NUMBER OF COMMANDS)

340 PO=PO+6
350 GOSUB 390
360 PRINT "END PO="PO", $"HEX$(PO)

370 SLOW

380 RETURN

# WE'RE PROCESSING THE INPUT FILE IN CPU-RAM
# ONCE A LINE IS DONE, WE EXECUTE RTV AS WE WOULD VMC

390 BW=PEEK(DB)+1:BH=PEEK(DB+1):ID=DB+2:PRINT"IMAGE DATA STARTS AT "ID

400 RO=0:WO=0:RC=0:CC=0:RT=AP:VT=0

410 POKE SP+4,7

420 FOR Y=0 TO BH-1
430  WO=Y*80
440  FOR X=0 TO BW-1
450   C$=RIGHT$(HEX$(PEEK(ID)),2)


#     WE FOUND A TRANSPARENT BYTE
460   IF INSTR(C$,"7")>0 THEN BEGIN

#      READ-COUNT>0 MEANS WE HAVE SOME BYTES IN THE QUEUE FOR VMC
470    IF RC>0 THEN GOSUB 1000

#      IF THE CURRENT BYTE IS ONLY PARTLY TRANSPARENT, WE DO AN RGO COMMAND
480    IF C$<>"77" THEN GOSUB 1100

490   BEND:GOTO 510

500   RC=RC+1:RO=RO+1

510  WO=WO+1:ID=ID+1:NEXT

520  IF RC>0 THEN GOSUB 1000

530 NEXT


540 POKE PO,0:PO=PO+1

#   METADATA LENGTH
542 ML=PO-SP

#   IMAGEDATA LENGTH
544 IL=RT-AP


#   WRITE METADATA-LENGTH
550 POKE SP,FNLB(ML):POKE SP+1,FNHB(ML)

#   WRITE IMAGEDATA-LENGTH
552 POKE SP+2,FNLB(IL):POKE SP+3,FNHB(IL)

554 POKE SP+5,CC

#   COPY TRANSPARENT IMAGE DATA TO RAM, AFTER COMMAND-DATA
560 VTR AP,PO,IL
565 BSAVE ("@"+PF$+".VT"),B0,P(SP)TOP(PO+IL)
#560 ATTR 0

570 VMS 8000,40,SP+4
580 END

590 RETURN



#880 POKE DB+IS,BW:POKE DB+IS+1,BH

#890 WF$="@"+LEFT$(PF$,LEN(PF$)-4)+".VDC"
#900 BSAVE (WF$),B0,U(DD),P(DB+IS) TO P(DB+IS+FL+PO)

#910 RETURN

600 DEF FN W(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256
610 DEF FN W4(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256+PEEK(ZZ+2)*65536+PEEK(ZZ+3)*16777216
620 DEF FN V(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))
630 DEF FN HN(ZZ)=DEC(MID$(HEX$(ZZ),3,1))
640 DEF FN LN(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))
650 DEF FN LB(ZZ)=DEC(RIGHT$(HEX$(ZZ),2))
660 DEF FN HB(ZZ)=DEC(LEFT$(HEX$(ZZ),2))

670 RETURN

# WRITE VMC DATA
1000 RS=ID-RC:RT=RT+VC
1005 VS=RO-RC
1010 VT=WO-RC
1015 VC=RC

1020 BANK 0
#    FIRST BYTE: VALUE 1 MEANS BLOCK-COPY DATA
1025 POKE PO,1:PO=PO+1
1030 POKE PO,FNLB(VT):PO=PO+1
1035 POKE PO,FNHB(VT):PO=PO+1
1040 POKE PO,FNLB(VC):PO=PO+1
1045 CC=CC+1
1050 RC=0
1055 RTV RS,RT,VC
1060 RETURN


#    WRITE RGO COMMAND
1100 BANK 0
1110 POKE PO,8:PO=PO+1
1120 POKE PO,FNLB(WO):PO=PO+1
1130 POKE PO,FNHB(WO):PO=PO+1
1140 CC=CC+1
1150 RS=ID:RT=RT+VC
1160 RTV RS,RT,1
1170 VC=1
1180 RETURN

